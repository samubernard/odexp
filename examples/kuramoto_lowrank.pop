## Kuramoto model of coupled oscillators
## rank 1 coupling matrix
## low rank coupling expansion 

# rank coupling matrix rank
let R 1

# number of oscillators
let N 100

# Coupling strength factor
par K 0.1

# CONSTANT
macro PI 3.141592

# EXPRESSIONS
# set frequency to normal distribution.
expr ω randN(2*PI/10.0,0.1) 

# factorization of the coupling matrix W = U*V
# U is 1xR and V is Rx1, row U are repeated and column V are repeated N times
expr U[i=0:R] 1.0
expr V[i=0:R] 1.0 

# mean fields/order parameters ()
# OMEGA: mean value of omega
%M Ω  ME(ω) 
%M MEANY  MA(y)

# AUXILIARY VARIABLES
aux y sin(θ)
aux theta θ - ( (int) (θ/2/PI) * 2 * PI )

# COUPLING
%C CPL lrwexp {var = theta, fun = sin, U = U[0], V = V[0], rank = R}

# DYNAMICAL VARIABLES 
dθ/dt = ω + K/N*CPL 

# INITIAL CONDITIONS
init θ 2*PI*rand01() 

# TIME SPAN
timespan 0 160  

# OPTIONS

# initial population size
opt popsize 1000

# plot MEANY on y-axis
opt y MEANY  

opt progress 2

